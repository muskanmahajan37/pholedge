buildscript {
    repositories {
        mavenCentral()
        jcenter()

        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://repo.spring.io/release" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.2.0.RELEASE"
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:2.1.3.RELEASE"
    }
}

plugins {
    id 'org.springframework.boot' version '2.2.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id "org.flywaydb.flyway" version "5.2.4"
    id 'checkstyle'
    id 'groovy'
    id 'maven'
}

apply plugin: 'spring-cloud-contract'

checkstyle {
    toolVersion = "8.25"
    ignoreFailures = false
    showViolations = true
}

group = 'com.phodal'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    jcenter()

    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://repo.spring.io/release" }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Greenwich.SR3"
        mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:2.1.3.RELEASE"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jersey'
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.1.0'

    implementation('org.springframework.cloud:spring-cloud-starter-openfeign')

    compile "org.flywaydb:flyway-core:5.2.4"
    compile group: 'com.google.guava', name: 'guava', version: '28.0-jre'
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'

    compileOnly 'org.projectlombok:lombok'

    compile 'mysql:mysql-connector-java'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'

    //test dependencies
    testImplementation(platform("org.junit:junit-bom:5.5.2"))

    testImplementation "org.junit.jupiter:junit-jupiter:5.5.2"

    // JUnit Vintage
    testCompileOnly('junit:junit:4.12')
    testRuntimeOnly('org.junit.vintage:junit-vintage-engine:5.5.2') {
        because 'allows JUnit 3 and JUnit 4 tests to run'
    }

    testRuntimeOnly("org.junit.platform:junit-platform-launcher") {
        because 'allows tests to run from IDEs that bundle older version of launcher'
    }

    testImplementation 'com.tngtech.archunit:archunit:0.11.0'
    testImplementation 'com.tngtech.archunit:archunit-junit5-api:0.11.0'
    testRuntimeOnly 'com.tngtech.archunit:archunit-junit5-engine:0.11.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
    testImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'

    testImplementation "io.rest-assured:spring-mock-mvc"

    testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:2.1.0'

    testImplementation 'org.mockito:mockito-core:3.1.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.1.0'
}

flyway {
    driver = 'com.mysql.cj.jdbc.Driver'
    url = "jdbc:mysql://localhost:3306/pholedge"
    user = 'root'
    password = 'password'
}

test {
    useJUnitPlatform {
         includeEngines 'junit-jupiter', 'junit-vintage'
        // includeTags 'fast'
        excludeTags 'slow'
    }

    testLogging {
        events 'passed', 'skipped', 'failed'
    }

}

contracts {
//    testFramework = "JUnit5"
    packageWithBaseClasses = 'pholedge'
    baseClassMappings {
        baseClassMapping(".*book.*", "com.phodal.pholedge.book.BookBase")
    }
}